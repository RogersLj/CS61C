### RISC-V 32个寄存器，寄存器大小为64位，64位叫双字，32位叫字

#### 设计原则：
- 更少则更快
  太多的寄存器会增加传输距离，是时钟周期变长


add
sub

ld 取双字
存放基址的寄存器x22被称为基址寄存器
sd 存储双字

计算机里都是字节寻址，所以偏移量是以字节为单位的

寄存器x0保存常数0

无符号字节载入lbu， 零扩展填充寄存器最左位

RISC-V指令都是32位长

所有的指令长度相同，但是对于不同的指令使用不同的指令格式

保持尽可能相似的指令格式会降低硬件的复杂性

在处理常量时，程序员不需要专门使用addi，汇编器会自动判断并声称正确的操作码和指令格式

AND与操作时某些位为0，会导致结果对应数的位为零，用到的源操作数通常被成为掩码，因为会隐藏某些位

### 一般来说，如果我们测试相反的条件来跳转，代码会更有效。bne指令一般比beq更有效 

![image-20211128171748134](https://raw.githubusercontent.com/RogersLj/Image/master/PicGo/image-20211128171748134.png)

![image-20211128171948665](https://raw.githubusercontent.com/RogersLj/Image/master/PicGo/image-20211128171948665.png)

![image-20211128182318143](https://raw.githubusercontent.com/RogersLj/Image/master/PicGo/image-20211128182318143.png)

- x10 - x17： 参数寄存器，用于传递参数或返回值

- x1： 返回地址寄存器，用于返回到过程开始前的初始地址
- x8 - x9, x18 - x27 保存寄存器，在过程调用的时候被被调用者保存

- x2： 栈指针



跳转链接指令 jal - jump and link

跳转到某个地址，同时将下一条命令的地址保存到目标寄存器中

存地址的目标寄存器里保存的就是过程结束后的返回地址

因为可能在程序不同的地方被调用，所以需要确定指令执行到哪里了，需要保存下一条指令的地址



被调用者在结束过程计算后，会把结果放在相同的参数寄存器中，然后

```assembly
jalr x0, 0(x1)
```

将控制返回给调用者，丢弃过程下面指令的地址，因为不需要保存

jal实际上就是把pc+8放在了寄存器x1中



如果 jar x0, Label

则无条件跳转，因为x0只读不能写，所以就会直接丢弃下面指令的地址



寄存器大小为64位，所以保存的时候需要8个字节的空间才能保存。栈指针以字节为单位，所以每次要+8或-8

每条RISC-V指令大小为32位，4字节，所以程序计数器每次需要+4或-4



栈是从高地址往低地址扩展



### 嵌套过程

计算阶乘

```c
long long int fact (long long int n)
{
	if (n < 1) return (1);
	else return (n * fact(n - 1));
}
```





汇编代码

参数变量n保存在x10

```assembly
fact:
	addi sp, sp, -16   //需要存放两个寄存器内容
	sd x1, 8(sp)   
	sd x10, 0(sp)
	
//第一次调用fact，会将返回地址和参数压入栈中

addi x5, x10, -1   //将n-1存入x5中
bge  x5, x0, L1
判断if n - 1 >= 0， n >= 1，跳转到L1

//(if (n < 1))
addi x10, x0, 1  //将结果保存在x10中，作为返回值
addi sp, sp, 16  //弹出栈
jalr x0, 0(x1)   //跳转到调用fact之前的指令的地方


//（if (n >= 1)）
L1:	addi x10, x10, -1
	jal x1, fact
将n-1，然后循环fact过程
注意压栈过程是在调用过程的时候，在过程里面完成的
但是存入x1的地址是在调用fact函数之前就已经完成的


addi  x6, x10, 0	//x10的内容放到x6中
ld    x10, 0(sp)	//恢复x10
ld    x1, 8(sp)		//恢复x1
addi  sp, sp(16)	//恢复栈指针

mul x10, x10, x6	//实际上x10存储的是1 * 2 * ... n的值

jalr x0, 0(x1)
```

如果第一次调用fact过程，且n < 1时，不会执行L1以及下面的指令，因为在(if (n < 1))分支保存变量并返回

而如果至少调用了两次fact，说明运行了（if (n >= 1)）分支，此时返回地址x1的地址为L1一下的指令地址



不断压栈，不断调用fact，直到最后n < 1后，才会不断弹栈，执行最后一个片段



# lab03

## exercise2

1. What do the `.data`, `.word`, `.text` directives mean (i.e. what do you use them for)? **Hint**: think about the 4 sections of memory.



.data 

声明全局变量的开始，在数据段



.word

按字分配空间，并放入变量值



.text

汇编代码的开始



在汇编代码里没有变量名，有的只有寄存器



```assembly
.data		#声明全局变量
.word 2, 4, 6, 8	#按字放入数据段中
n: .word 9		##声明变量n=9

.text
main:
    add t0, x0, x0	#清零寄存器t0(x5)	- t0 = 0
    addi t1, x0, 1	#t1(x6)寄存器置为1	- t1 = 1 
    la t3, n		#将n的地址放到寄存器t3(x28)里
    # 地址是32位，所以la是由两句指令组成
    # 先加载高20位，再低12位
    lw t3, 0(t3)	#将存储器取字到寄存器，也就是将n存到t3里
fib:
    beq t3, x0, finish #if (n = 0) finish
    add t2, t1, t0		#t2 = 1
    mv t0, t1			#t0 = 1
    mv t1, t2			# t1 =1 
    addi t3, t3, -1		# n -= 1	- t3 = 8
    j fib
finish:
    addi a0, x0, 1		# a0 = 1
    addi a1, t0, 0		# a1 = f(9)
    ecall # print integer ecall
    addi a0, x0, 10
    ecall # terminate ecall
```



最开始t0 = 0, t1 = 1

t2 = t0 + t1

F(0) = 0, F(1) = 1

f(2) = F(0) + F(1)

mv t0, t1

mv t1, t2



fib循环一次，t0指向f(1)



n会从9一直减到1，知道最后0不再进入循环

一共会执行9次

最后停在位置F(9)

t0 = F(n)





---

## Load Address

The following example shows the `la` pseudo instruction which is used to load symbol addresses:

```assembly
	la	a0, msg + 1
```

---



| mv rd, rs     | addi rd, rs, 0                                     | Copy register |
| ------------- | -------------------------------------------------- | ------------- |
| la rd, symbol | auipc rd, symbol[31:12]; addi rd, rd, symbol[11:0] | Load address  |

| mv rd, rs | addi rd, rs, 0 | Copy register |
| --------- | -------------- | ------------- |
|           |                |               |



la rd, symbol

la伪指令其实是由两条指令组成，因为指令中立即数大小有限制，所以需要两条指令加载地址

- `auipc`: Add upper immediate to PC. This instruction adds a 20-bit immediate value to the upper 20 bits of the program counter. This instruction enables PC-relative addressing in RISC-V. To form a complete 32-bit PC-relative address, `auipc` forms a partial result, then an `addi` instruction adds in the lower 12 bits.



# exercise 3

```c
int source[] = {3, 1, 4, 1, 5, 9, 0};
int dest[10];

int fun(int x) {
	return -x * (x + 1);
}

int main() {
    int k;
    int sum = 0;
    for (k = 0; source[k] != 0; k++) {
        dest[k] = fun(source[k]);
        sum += dest[k];
    }
    return sum;
}
```





```assembly
.globl main

.data
source:
    .word   3
    .word   1
    .word   4
    .word   1
    .word   5
    .word   9
    .word   0
dest:
    .word   0
    .word   0
    .word   0
    .word   0
    .word   0
    .word   0
    .word   0
    .word   0
    .word   0
    .word   0

.text
fun:
    addi t0, a0, 1		#x + 1	
    sub t1, x0, a0		#-x
    mul a0, t0, t1		#-x * (x + 1)
    jr ra

main:
    # BEGIN PROLOGUE
    addi sp, sp, -20
    sw s0, 0(sp)
    sw s1, 4(sp)
    sw s2, 8(sp)
    sw s3, 12(sp)
    sw ra, 16(sp)		#保存寄存器和返回地址压入栈中
    # END PROLOGUE
    addi t0, x0, 0		#t0 = 0
    addi s0, x0, 0		#s0 = 0
    la s1, source		#s1 - source数组的起始地址 - 就像指针
    la s2, dest			#s2 - dest数组起始地址 - 就像指针
loop:
    slli s3, t0, 2		#s3 = t0 << 2，因为每个int是4个字节，地址每次偏移4
    add t1, s1, s3		#t1 = s1 + s3 - source数组的起始地址加上偏移量
    lw t2, 0(t1)		#t2 = source[s3]
    beq t2, x0, exit
    add a0, x0, t2		#a0 存放参数 source[s3]
    addi sp, sp, -8
    sw t0, 0(sp)
    sw t2, 4(sp)
    jal fun				#执行fun(source[k])
    lw t0, 0(sp)
    lw t2, 4(sp)
    addi sp, sp, 8
    add t2, x0, a0		#t2为fun返回值
    add t3, s2, s3		#dest相应偏移量
    sw t2, 0(t3)
    add s0, s0, t2		#sum += dest[k]
    addi t0, t0, 1		#k++
    jal x0, loop
exit:
    add a0, x0, s0
    # BEGIN EPILOGUE
    lw s0, 0(sp)
    lw s1, 4(sp)
    lw s2, 8(sp)
    lw s3, 12(sp)
    lw ra, 16(sp)
    addi sp, sp, 20
    # END EPILOGUE
    jr ra
```





x2存栈指针

x1函数返回地址







# exercise 4

```assembly
.globl factorial

.data
n: .word 8

.text
main:
    la t0, n
    lw a0, 0(t0)
    jal ra, factorial

    addi a1, a0, 0
    addi a0, x0, 1
    ecall # Print Result

    addi a1, x0, '\n'
    addi a0, x0, 11
    ecall # Print newline

    addi a0, x0, 10
    ecall # Exit

factorial:
    # YOUR CODE HERE
    add t1, x0, a0	#t1 - counts from n to 1
    addi t2, x0, 1  #t2 = result
    
loop:
	ble t1, x0, finish 		#(n <= 0)
    mul t2, t2, t1			# res = res * n
    addi t1, t1, -1			# n --
    jal x0, loop
    
finish:
	mv a0, t2	#将结果存到保存寄存器里
	jr ra
```







# exercise 5

```assembly
.globl map

.text
main:
    jal ra, create_default_list
    add s0, a0, x0 # a0 (and now s0) is the head of node list
    #s0是list头节点的地址

    # Print the list
    add a0, s0, x0
    jal ra, print_list
    # Print a newline
    jal ra, print_newline

    # === Calling `map(head, &square)` ===
    # Load function arguments
    add a0, s0, x0 # Loads the address of the first node into a0
    #a0 - 头结点的位置

    # Load the address of the "square" function into a1 (hint: check out "la" on the green sheet)
    ### YOUR CODE HERE ###
	la a1, square	#a1是square函数的地址

    # Issue the call to map
    jal ra, map		#调用map

    # Print the squared list
    add a0, s0, x0
    jal ra, print_list
    jal ra, print_newline

    # === Calling `map(head, &decrement)` === map(a0, a1)
    # Because our `map` function modifies the list in-place, the decrement takes place after
    # the square does

    # Load function arguments
    add a0, s0, x0 # Loads the address of the first node into a0
    #s0是list头节点的地址,存在a0里
    
    # Load the address of the "decrement" function into a1 (should be very similar to before)
    ### YOUR CODE HERE ###
    la a1, decrement

    # Issue the call to map
    jal ra, map

    # Print decremented list
    add a0, s0, x0
    jal ra, print_list
    jal ra, print_newline

    addi a0, x0, 10
    ecall # Terminate the program

map:
    # Prologue: Make space on the stack and back-up registers
    ### YOUR CODE HERE ###
    #调用map函数，将之前的参数寄存器和返回地址压栈，返回地址在最上面
    addi sp, sp, -12
    sw 	ra, 0(sp)
    sw	s0, 4(sp)
    sw	s1, 8(sp) 	#后面会用到s1，防止s1内容丢失，也将其压入栈中
    
    
loop1:
    beq a0, x0, done # If we were given a null pointer (address 0), we're done.
    # if (head == NULL) return

    add s0, a0, x0 # Save address of this node in s0 - s0 = 头结点地址
    add s1, a1, x0 # Save address of function in s1	- s1 = square函数地址

    # Remember that each node is 8 bytes long: 4 for the value followed by 4 for the pointer to next. - 地址的前4个字节是value，后四个字节是指针
    # What does this tell you about how you access the value and how you access the pointer to next?

    # Load the value of the current node into a0 - 将s0里面地址的前四个字节内容加载到a0
    # THINK: Why a0? - a0是参数寄存器，交给square函数并返回
    ### YOUR CODE HERE ###
    lw a0, 0(s0)
    
    # Call the function in question on that value. DO NOT use a label (be prepared to answer why).
    # Hint: Where do we keep track of the function to call? Recall the parameters of "map".
    ### YOUR CODE HERE ###
    jalr ra, s1, 0

    # Store the returned value back into the node - s0地址的前四个字节
    # Where can you assume the returned value is? - a0
    ### YOUR CODE HERE ###
    sw a0, 0(s0)

    # Load the address of the next node into a0
    # The address of the next node is an attribute of the current node.
    # Think about how structs are organized in memory.
    ### YOUR CODE HERE ###
    lw a0, 4(s0)

    # Put the address of the function back into a1 to prepare for the recursion
    # THINK: why a1? What about a0? - a0是下一个结点的地址，将交给square函数循环处理;a1是square函数的地址,是需要传递到下一次循环里调用的参数
    ### YOUR CODE HERE ###
	add a1, x0, s1
    
    # Recurse
    ### YOUR CODE HERE ###
    j loop1

done:
    # Epilogue: Restore register values and free space from the stack
    ### YOUR CODE HERE ### - 怎么入栈，出栈就是一样的，函数结束即出栈
    lw ra, 0(sp)
    lw s0, 4(sp)
    lw s1, 8(sp)
    addi sp, sp, 12

    jr ra # Return to caller

# === Definition of the "square" function ===
square:
    mul a0, a0, a0
    jr ra

# === Definition of the "decrement" function ===
decrement:
    addi a0, a0, -1
    jr ra

# === Helper functions ===
# You don't need to understand these, but reading them may be useful

create_default_list:
    addi sp, sp, -12
    sw ra, 0(sp)
    sw s0, 4(sp)
    sw s1, 8(sp)
    li s0, 0            # Pointer to the last node we handled
    li s1, 0            # Number of nodes handled
loop:                   # do...
    li a0, 8
    jal ra, malloc      #     Allocate memory for the next node
    sw s1, 0(a0)        #     node->value = i
    sw s0, 4(a0)        #     node->next = last
    add s0, a0, x0      #     last = node
    addi s1, s1, 1      #     i++
    addi t0, x0, 10
    bne s1, t0, loop    # ... while i!= 10
    lw ra, 0(sp)
    lw s0, 4(sp)
    lw s1, 8(sp)
    addi sp, sp, 12
    jr ra

print_list:
    bne a0, x0, print_me_and_recurse
    jr ra               # Nothing to print
print_me_and_recurse:
    add t0, a0, x0      # t0 gets current node address
    lw a1, 0(t0)        # a1 gets value in current node
    addi a0, x0, 1      # Prepare for print integer ecall
    ecall
    addi a1, x0, ' '    # a0 gets address of string containing space
    addi a0, x0, 11     # Prepare for print char syscall
    ecall
    lw a0, 4(t0)        # a0 gets address of next node
    jal x0, print_list  # Recurse. The value of ra hasn't been changed.

print_newline:
    addi a1, x0, '\n'   # Load in ascii code for newline
    addi a0, x0, 11
    ecall
    jr ra

malloc:
    addi a1, a0, 0
    addi a0, x0, 9
    ecall
    jr ra

```









---

.align的作用在于对指令或者数据的存放地址进行对齐



### 加载大立即数

## LUI/ADDI Address Calculation in RISC-V

 如果string的地址是 0x00020 A10

按理说我们应该执行的两条指令是

 LUI 0x00020, ADDI 0xA10

分别加载高20位和低20位

但是，立即数在RISC-V里是符号填充的

意思是上面的指令会导致

0x00020 000 + 0xFFFFF A10 = 0x0001F A10 



所以如果我们想我们低12位地址 0xFFFFF A10变为0x00000 A10

就需要加上0x0000 1 000作为偏移量

我们需要将偏移量加到lui加载的常量上面

也就是 

(0x00020 000 + 0x00001 000) + 0xFFFFF A10 = 0x00020 A10



所以最终的两条指令应该是

a0,0x21

a0,a0,-1520 



其中 0xFFFFF A10 = 0x00000 5EF + 1 = 0x00000 5F0 = 1520ten



## 关于RISC-V里数的载入

当把一个64位双字载入一个64位寄存器中时，上述讨论是无意义的，此时有符号数和无符号数的载入是相同的。RISC-V确实提供了**两种**字节载入方式：无符号字节载入（**1bu**）将字节视为无符号数，因此用**零扩展**填充寄存器的最左位，而字节载入（1b）使用带符号整数。由于C程序几乎总是使用字节来表示字符，而不是将字节视为有符号短整数，所以1bu实际上专门用于字节载入。
